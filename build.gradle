plugins {
    id 'groovy'
    id 'idea'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}
apply plugin: 'maven-publish'

ext {
    url = 'https://github.com/Exploding-Creeper/CurseGradle'
}

version = '2.0.2'
group = 'me.hypherionmc'
description = 'Gradle plugin to upload artifacts to CurseForge'
archivesBaseName = 'CurseGradle'

if (System.getenv('BUILD_NUMBER') != null) {
    version = "2.0." + System.getenv('BUILD_NUMBER')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()

    implementation group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
}

jar.manifest.mainAttributes([
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.version
])

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

assemble.dependsOn javadocJar, sourcesJar

[compileJava, compileGroovy].each {
    it.options.encoding = 'UTF-8'
    it.options.deprecation = true
    it.options.fork = true
}

pluginBundle {
    website = project.url
    vcsUrl = 'https://github.com/Exploding-Creeper/CurseGradle'
    plugins {
        curseGradlePlugin {
            id = 'me.hypherionmc.cursegradle'
            description = project.description
            displayName = 'CurseGradle'
            version = project.version
            tags = ['curseforge', 'publish', 'minecraft']
        }
    }
    mavenCoordinates {
        groupId = 'me.hypherionmc.cursegradle'
        artifactId = "CurseGradle"
        version = project.version
    }
}

shadowJar {
    classifier ''
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
            artifact javadocJar
            artifact sourcesJar
        }
    }

    repositories {
        maven {
            url "https://maven.explodingcreeper.me/"
            credentials {
                username System.getenv('MAVEN_USER')
                password System.getenv('MAVEN_PASS')
            }
        }
    }
}
